* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-12-29
* https://github.com/Aleksandr3Bocharov/refalab

*====================================================================
* Рефал-функции библиотеки РефалАБ.
*====================================================================

COMP2 +
    START

    ENTRY LTN, LEN, EQN, NEN, GEN, GTN
    ENTRY QSortN, MinN, MaxN
    ENTRY LTL, LEL, EQL, NEL, GEL, GTL
    ENTRY QSortW, MinW, MaxW
    EXTRN NNorm, NCheck, UnBrackets, FoldL, NRel, LRel, True, False

    IMPL

* <LTN (E.N1) E.N2> = &True | &False
LTN E._ +
        = <LTN_Aux <NRel E._>>

LTN_Aux
    '<' E._ +
        = &True
    E._ = &False

* <LEN (E.N1) E.N2> = &True | &False
LEN E._ +
        = <LEN_Aux <NRel E._>>

LEN_Aux
    '>' E._ +
        = &False
    E._ = &True

* <EQN (E.N1) E.N2> = &True | &False
EQN E._ +
        = <EQN_Aux <NRel E._>>

EQN_Aux
    '=' E._ +
        = &True
    E._ = &False

* <NEN (E.N1) E.N2> = &True | &False
NEN E._ +
        = <NEN_Aux <NRel E._>>

NEN_Aux
    '=' E._ +
        = &False
    E._ = &True

* <GEN (E.N1) E.N2> = &True | &False
GEN E._ +
        = <GEN_Aux <NRel E._>>

GEN_Aux
    '<' E._ +
        = &False
    E._ = &True

* <GTN (E.N1) E.N2> = &True | &False
GTN E._ +
        = <GTN_Aux <NRel E._>>

GTN_Aux
    '>' E._ +
        = &True
    E._ = &False

* <QSortN E.AnyNumbers> == E.Sort_AnyNumbers
QSortN
        =
    W.Pivot +
        = (<NCheck <Unbrackets W.Pivot>>)
    W.Pivot E.Items +
        = <QSortN_Aux <FoldL &DoQSortN (() (W.Pivot) ()) E.Items>>

QSortN_Aux
    ((E.Less) (E.Equal) (E.Greater)) +
        = <QSortN E.Less> E.Equal <QSortN E.Greater>

DoQSortN
    (W.Less ((E.Pivot) E.Equal) W.Greater) W.Next +
        = <DoQSortN_Aux <NRel W.Next E.Pivot> +
            (W.Less ((E.Pivot) E.Equal) W.Greater)>

DoQSortN_Aux
    '<' W.Next E._ ((E.Less) E.__) +
        = ((E.Less W.Next) E.__)
    '=' W.Next E._ (W.Less (E.Equal) W.Greater) +
        = (W.Less (E.Equal W.Next) W.Greater)
    '>' W.Next E._ (E.__ (E.Greater)) +
        = (E.__ (E.Greater W.Next))

* <MinN (E.First) (E.Number)*> == V.Min
MinN
    W.First E.Tail +
        = <NNorm <UnBrackets <FoldL &DoMinN W.First E.Tail>>>

DoMinN
    W.X (E.Y) +
        = <DoMinN_Aux <NRel W.X E.Y>>

DoMinN_Aux
    '<' W.X E._ +
        = W.X
    S._ W.__ E.Y +
        = (E.Y)

* <MaxN (E.First) (E.Number)*> == V.Max
MaxN
    W.First E.Tail +
        = <NNorm <UnBrackets <FoldL &DoMaxN W.First E.Tail>>>

DoMaxN
    W.X (E.Y) +
        = <DoMaxN_Aux <NRel W.X E.Y>>

DoMaxN_Aux
    '>' W.X E._ +
        = W.X
    S._ W.__ E.Y +
        = (E.Y)

* <LTL (E.E1) E.E2> = &True | &False
LTL E._ +
        = <LTL_Aux <LRel E._>>

LTL_Aux
    '<' = &True
    E._ = &False

* <LEL (E.E1) E.E2> = &True | &False
LEL E._ +
        = <LEL_Aux <LRel E._>>

LEL_Aux
    '>' = &False
    E._ = &True

* <EQL (E.E1) E.E2> = &True | &False
EQL E._ +
        = <EQL_Aux <LRel E._>>

EQL_Aux
    '=' = &True
    E._ = &False

* <NEL (E.E1) E.E2> = &True | &False
NEL E._ +
        = <NEL_Aux <LRel E._>>

NEL_Aux
    '=' = &False
    E._ = &True

* <GEL (E.E1) E.E2> = &True | &False
GEL E._ +
        = <GEL_Aux <LRel E._>>

GEL_Aux
    '<' = &False
    E._ = &True

* <GTL (E.E1) E.E2> = &True | &False
GTL E._ +
        = <GTL_Aux <LRel E._>>

GTL_Aux
    '>' = &True
    E._ = &False

* <QSortW E.Any> == E.Sort_Any
QSortW
        =
    W.Pivot +
        = W.Pivot
    W.Pivot E.Items +
        = <QSortW_Aux <FoldL &DoQSortW (() (W.Pivot) ()) E.Items>>

QSortW_Aux
    ((E.Less) (E.Equal) (E.Greater)) +
        = <QSortW E.Less> E.Equal <QSortW E.Greater>

DoQSortW
    (W.Less (W.Pivot E.Equal) W.Greater) W.Next +
        = <DoQSortW_Aux <LRel (W.Next) W.Pivot> +
            (W.Less (W.Pivot E.Equal) W.Greater) W.Next>

DoQSortW_Aux
    '<' ((E.Less) E._) W.Next +
        = ((E.Less W.Next) E._)
    '=' (W.Less (E.Equal) W.Greater) W.Next +
        = (W.Less (E.Equal W.Next) W.Greater)
    '>' (E._ (E.Greater)) W.Next +
        = (E._ (E.Greater W.Next))

* <MinW W.First E.Tail> == W.Min
MinW
    W.X W.Y +
        = <MinW_Aux <LRel (W.X) W.Y> W.X W.Y>
    W.First E.Tail +
        = <FoldL &MinW W.First E.Tail>

MinW_Aux
    '<' W.X E._ +
        = W.X
    E._ W.Y +
        = W.Y

* <MaxW W.First E.Tail> == W.Max
MaxW
    W.X W.Y +
        = <MaxW_Aux <LRel (W.X) W.Y> W.X W.Y>
    W.First E.Tail +
        = <FoldL &MaxW W.First E.Tail>

MaxW_Aux
    '>' W.X E._ +
        = W.X
    E._ W.Y +
        = W.Y

    END
