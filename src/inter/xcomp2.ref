* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-11-05
* https://github.com/Aleksandr3Bocharov/refalab

*====================================================================
* Рефал-функции библиотеки РефалАБ.
*====================================================================

COMP2 +
    START

    ENTRY MinN
    ENTRY QSortW, MinW, MaxW
    EXTRN FoldL, NRel, LRel

    IMPL

* <QSortW E.Any> == E.Sort_Any
QSortW
        =
    W.Pivot +
        = W.Pivot
    W.Pivot E.Items +
        = <QSortW_Aux <FoldL &DoQSortW (() (W.Pivot) ()) E.Items>>

QSortW_Aux
    ((E.Less) (E.Equal) (E.Greater)) +
        = <QSortW E.Less> E.Equal <QSortW E.Greater>

DoQSortW
    ((E.Less) (W.Pivot E.Equal) (E.Greater)) W.Next +
        = <DoQSortW_Aux <LRel (W.Next) W.Pivot> +
            ((E.Less) (W.Pivot E.Equal) (E.Greater)) W.Next>

DoQSortW_Aux
    '<' ((E.Less) E._) W.Next +
        = ((E.Less W.Next) E._)
    '=' ((E.Less) (E.Equal) (E.Greater)) W.Next +
        = ((E.Less) (E.Equal W.Next) (E.Greater))
    '>' (E._ (E.Greater)) W.Next +
        = (E._ (E.Greater W.Next))

* <MinN (E.Min)+> == V.Min
MinN
    W.First E.Tail +
        = <FoldL &DoMinN W.First E.Tail>

DoMinN
    W.X (E.Y) +
        = <DoMinN_Aux <NRel W.X E.Y>>

DoMinN_Aux
    '<' W.X E._ +
        = W.X
    S._ W.__ E.Y +
        = (E.Y)

* <MinW W.Min+> == W.Min
MinW
    W.X W.Y +
        = <MinW_Aux <LRel (W.X) W.Y> W.X W.Y>
    W.First E.Tail +
        = <FoldL &MinW W.First E.Tail>

MinW_Aux
    '<' W.X E._ +
        = W.X
    E._ W.Y +
        = W.Y

* <MaxW W.Max+> == W.Max
MaxW
    W.X W.Y +
        = <MaxW_Aux <LRel (W.X) W.Y> W.X W.Y>
    W.First E.Tail +
        = <FoldL &MaxW W.First E.Tail>

MaxW_Aux
    '>' W.X E._ +
        = W.X
    E._ W.Y +
        = W.Y

    END
