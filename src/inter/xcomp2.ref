* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-11-10
* https://github.com/Aleksandr3Bocharov/refalab

*====================================================================
* Рефал-функции библиотеки РефалАБ.
*====================================================================

COMP2 +
    START

    ENTRY QSortN, MinN, MaxN
    ENTRY QSortW, MinW, MaxW
    EXTRN NNorm, UnBrackets, FoldL, NRel, LRel

    IMPL

* <QSortN E.AnyNumbers> == E.Sort_AnyNumbers
QSortN
        =
    W.Pivot +
        = W.Pivot
    W.Pivot E.Items +
        = <QSortN_Aux <FoldL &DoQSortN (() (W.Pivot) ()) E.Items>>

QSortN_Aux
    ((E.Less) (E.Equal) (E.Greater)) +
        = <QSortN E.Less> E.Equal <QSortN E.Greater>

DoQSortN
    ((E.Less) ((E.Pivot) E.Equal) (E.Greater)) W.Next +
        = <DoQSortN_Aux <LRel W.Next E.Pivot> +
            ((E.Less) ((E.Pivot) E.Equal) (E.Greater))>

DoQSortN_Aux
    '<' W.Next E._ ((E.Less) E.__) +
        = ((E.Less W.Next) E.__)
    '=' W.Next E._ ((E.Less) (E.Equal) (E.Greater)) +
        = ((E.Less) (E.Equal W.Next) (E.Greater))
    '>' W.Next E._ (E.__ (E.Greater)) +
        = (E.__ (E.Greater W.Next))

* <MinN (E.First) (E.Number)+> == V.Min
MinN
    W.First V.Tail +
        = <NNorm <UnBrackets <FoldL &DoMinN W.First V.Tail>>>

DoMinN
    W.X (E.Y) +
        = <DoMinN_Aux <NRel W.X E.Y>>

DoMinN_Aux
    '<' W.X E._ +
        = W.X
    S._ W.__ E.Y +
        = (E.Y)

* <MaxN (E.First) (E.Number)+> == V.Max
MaxN
    W.First V.Tail +
        = <NNorm <UnBrackets <FoldL &DoMaxN W.First V.Tail>>>

DoMaxN
    W.X (E.Y) +
        = <DoMaxN_Aux <NRel W.X E.Y>>

DoMaxN_Aux
    '>' W.X E._ +
        = W.X
    S._ W.__ E.Y +
        = (E.Y)

* <QSortW E.Any> == E.Sort_Any
QSortW
        =
    W.Pivot +
        = W.Pivot
    W.Pivot E.Items +
        = <QSortW_Aux <FoldL &DoQSortW (() (W.Pivot) ()) E.Items>>

QSortW_Aux
    ((E.Less) (E.Equal) (E.Greater)) +
        = <QSortW E.Less> E.Equal <QSortW E.Greater>

DoQSortW
    (W.Less (W.Pivot E.Equal) W.Greater) W.Next +
        = <DoQSortW_Aux <LRel (W.Next) W.Pivot> +
            (W.Less (W.Pivot E.Equal) W.Greater) W.Next>

DoQSortW_Aux
    '<' ((E.Less) E._) W.Next +
        = ((E.Less W.Next) E._)
    '=' (W.Less (E.Equal) W.Greater) W.Next +
        = (W.Less (E.Equal W.Next) W.Greater)
    '>' (E._ (E.Greater)) W.Next +
        = (E._ (E.Greater W.Next))

* <MinW W.First V.Tail> == W.Min
MinW
    W.X W.Y +
        = <MinW_Aux <LRel (W.X) W.Y> W.X W.Y>
    W.First V.Tail +
        = <FoldL &MinW W.First V.Tail>

MinW_Aux
    '<' W.X E._ +
        = W.X
    E._ W.Y +
        = W.Y

* <MaxW W.First V.Tail> == W.Max
MaxW
    W.X W.Y +
        = <MaxW_Aux <LRel (W.X) W.Y> W.X W.Y>
    W.First V.Tail +
        = <FoldL &MaxW W.First V.Tail>

MaxW_Aux
    '>' W.X E._ +
        = W.X
    E._ W.Y +
        = W.Y

    END
