* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2026-01-02
* https://github.com/Aleksandr3Bocharov/refalab

*====================================================================
* Рефал-функции библиотеки РефалАБ.
* Пустые функции библиотеки РефалАБ.
* Спецификаторы библиотеки РефалАБ.
*====================================================================

BOOL +
    START

    ENTRY True, False, Bool
    ENTRY And, Or, Not
    EMPTY True, False

Bool +
    S   &True &False

    IMPL

* <And E(:Bool: B).BExp> = &True | &False
And E(:Bool: B).BExp +
        = <And_Aux E.BExp>

And_Aux = &True
    &False E._ +
        = &False
    &True E.BExp +
        = <And_Aux E.BExp>
    (E.Exp) E.BExp_E +
        = <And_Aux <BExp_Check < E.Exp>> E.BExp_E>

BExp_Check
    E(:Bool: B).BExp +
        = E.BExp

* <Or E(:Bool: B).BExp> = &True | &False
Or   E(:Bool: B).BExp +
        = <Or_Aux E.BExp>

Or_Aux  = &False
    &True E._ +
        = &True
    &False E.BExp +
        = <Or_Aux E.BExp>
    (E.Exp) E.BExp_E +
        = <Or_Aux <BExp_Check < E.Exp>> E.BExp_E>

* <Not S(:Bool:).Bool> = &True | &False
Not &True +
        = &False
    &False +
        = &True

    END
