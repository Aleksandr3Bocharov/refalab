* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-12-09
* https://github.com/Aleksandr3Bocharov/refalab

*====================================================================
* Рефал-функции библиотеки РефалАБ.
* Спецификаторы библиотеки РефалАБ.
*====================================================================

AR2 START

    ENTRY Inc, Dec
    ENTRY Max_Number, Get_Max_Number, Is_Max_Number
    ENTRY NNorm, NCheck, Abs
    EXTRN Add, Sub
    EXTRN True, False

Max_Number +
    S   4294967295

    IMPL

* <Inc E.Number*> == E.Number1
* E.Number ::= '+' S(N).Number+ | '-' S(N).Number+ | S(N).Number*
* E.Number1 ::= '-'? S(N).Number+
Inc E.Number +
        = <Add (E.Number) 1>

* <Dec E.Number*> == E.Number1
* E.Number ::= '+' S(N).Number+ | '-' S(N).Number+ | S(N).Number*
* E.Number1 ::= '-'? S(N).Number+
Dec E.Number +
        = <Sub (E.Number) 1>

* <Get_Max_Number> == 4294967295
Get_Max_Number
        = 4294967295

* <Is_Max_Number S.Any> == &True | &False
Is_Max_Number
    S(:Max_Number:)._ +
        = &True
    S._ = &False

* <NNorm E.Number> = V.NormNumber
NNorm   = 0
    '-' V(0).Zeros +
        = 0
    R '-' E(0).Zeros V(N).Number +
        = '-' V.Number
    R '+' E(0).Zeros V(N).Number +
        = V.Number
    R E(0).Zeros V(N).Number +
        = V.Number

* <NCheck E.Number> = E.Number
NCheck
    S('+-').Znak V(N).Number +
        = S.Znak V.Number
    E(N).Number +
        = E.Number

* <Abs E.Number> = V.AbsNumber
Abs     = 0
    R '-' E(0).Zeros V(N).Number +
        = V.Number
    R '+' E(0).Zeros V(N).Number +
        = V.Number
    R E(0).Zeros V(N).Number +
        = V.Number

    END
